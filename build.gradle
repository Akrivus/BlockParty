buildscript {
  repositories {
    maven { url = 'https://files.minecraftforge.net/maven' }
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow', version: '4.0.4'
  }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'

version = new Date().format('yy.MM.dd')
archivesBaseName = 'MoeBlocks'
group = 'moeblocks'

minecraft {
  mappings channel: 'snapshot', version: '20201028-1.16.3'

  runs {
    client {
      mods { moeblocks { source sourceSets.main } }
      workingDirectory project.file('run')
    }
    data {
      args '--mod', 'moeblocks', '--all', '--output', file('src/generated/resources/')
      mods { moeblocks { source sourceSets.main } }
      workingDirectory project.file('run')
    }
    server {
      mods { moeblocks { source sourceSets.main } }
      workingDirectory project.file('run')
    }
  }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
  minecraft 'net.minecraftforge:forge:1.16.5-36.0.4'
  shadow 'org.xerial:sqlite-jdbc:3.34.0'
  implementation 'org.xerial:sqlite-jdbc:3.34.0'
}

shadowJar {
  archiveName = "${archivesBaseName}-${version}.jar"
  configurations = [project.configurations.shadow]
}

reobf {
  shadowJar { }
}

tasks.build.dependsOn reobfShadowJar
jar.finalizedBy('reobfShadowJar')

jar {
  manifest {
    attributes([
      "Implementation-Title"    :  archivesBaseName,  "Specification-Title"   : archivesBaseName,
      "Implementation-Vendor"   : "Owen Adrian",      "Specification-Vendor"  : "Owen Adrian",
      "Implementation-Version"  :  version,           "Specification-Version" : "1",
      "Implementation-Timestamp":  new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
  }
}

publishing {
  repositories { maven { url "file:///${project.projectDir}/mcmodsrepo" } }
  publications { mavenJava(MavenPublication) { artifact jar } }
}
