buildscript {
  repositories {
    maven { url = 'https://files.minecraftforge.net/maven' }
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1+', changing: true
    classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow', version: '4.0.4'
  }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'

apply from: 'https://raw.githubusercontent.com/SizableShrimp/Forge-Class-Remapper/main/classremapper.gradle'

version = new Date().format('yy.MM.dd')
archivesBaseName = 'BlockParty'
group = 'block_party'

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

minecraft {
  mappings channel: 'official', version: '1.17.1'

  runs {
    client {
      mods { block_party { source sourceSets.main } }
      workingDirectory project.file('run')
    }
    data {
      args '--mod', 'block_party', '--all', '--output', file('src/generated/resources/')
      mods { block_party { source sourceSets.main } }
      workingDirectory project.file('run')
    }
    server {
      mods { block_party { source sourceSets.main } }
      workingDirectory project.file('run')
    }
  }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
  minecraft 'net.minecraftforge:forge:1.17.1-37.0.32'
  shadow 'org.xerial:sqlite-jdbc:3.34.0'
  implementation 'org.xerial:sqlite-jdbc:3.34.0'
}

shadowJar {
  archiveName = "${archivesBaseName}-${version}.jar"
  configurations = [project.configurations.shadow]
}

reobf {
  shadowJar { }
}

tasks.build.dependsOn reobfShadowJar
jar.finalizedBy('reobfShadowJar')

jar {
  manifest {
    attributes([
      "Implementation-Title"    :  archivesBaseName,  "Specification-Title"   : archivesBaseName,
      "Implementation-Vendor"   : "Owen Adrian",      "Specification-Vendor"  : "Owen Adrian",
      "Implementation-Version"  :  version,           "Specification-Version" : "1",
      "Implementation-Timestamp":  new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
  }
}

publishing {
  repositories { maven { url "file:///${project.projectDir}/mcmodsrepo" } }
  publications { mavenJava(MavenPublication) { artifact jar } }
}
