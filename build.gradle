buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:5.1.+'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

apply plugin: 'net.minecraftforge.gradle'

version = new Date().format('yy.MM.dd')
group = 'block_party'
archivesBaseName = 'BlockParty'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: '1.19.4'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        server {}
        client {}
        all {
            mods { block_party { source sourceSets.main } }
            workingDirectory file('run')
            lazyToken('minecraft_classpath') {
                configurations.integrate.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
            }
        }
        data {
            args '--mod', 'block_party', '--all', '--output', file('src/generated/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    integrate
    implementation.extendsFrom integrate
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.4-45.2.0'
    integrate 'org.xerial:sqlite-jdbc:3.40.1.0'
}

shadowJar {
    relocate 'org.sqlite', 'block_party.org.sqlite'

    archiveName("${archivesBaseName}-${project.version}.jar")
    configurations = [project.configurations.integrate]
}

reobf {
    shadowJar {}
}

tasks.build.dependsOn shadowJar
jar.finalizedBy('reobfShadowJar')

artifacts {
    archives tasks.shadowJar
}

repositories {
    mavenCentral()
}