buildscript {
  repositories {
    maven { url = 'https://files.minecraftforge.net/maven' }
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
  }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'

version = new Date().format('yy.MM.dd')
archivesBaseName = 'MoeBlocks'
group = 'moe.blocks.mod'

minecraft {
  mappings channel: 'snapshot', version: '20201028-1.16.3'

  runs {
    client {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
      property 'forge.logging.console.level', 'debug'
      mods { moeblocks { source sourceSets.main } }
    }
    server {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
      property 'forge.logging.console.level', 'debug'
      mods { moeblocks { source sourceSets.main } }
    }
  }
}

dependencies {
  minecraft 'net.minecraftforge:forge:1.16.4-35.0.7'
  compile files('libs/jruby-complete-9.2.13.0.jar')
}

jar {
  manifest {
    attributes([
        "Implementation-Title": project.name, "Implementation-Vendor": "Owen Adrian",
        "Implementation-Version": "${version}",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        "Specification-Title": "MoeBlocks", "Specification-Vendor": "Owen Adrian",
        "Specification-Version": "1"
    ])
  }
  from {
    files('libs/jruby-complete-9.2.13.0.jar').collect() { zipTree(it) }
  }
}

jar.finalizedBy('reobfJar')

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact jar
    }
  }
  repositories {
    maven {
      url "file:///${project.projectDir}/mcmodsrepo"
    }
  }
}